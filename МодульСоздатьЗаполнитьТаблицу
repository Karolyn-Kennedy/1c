//Функция - Создать таблицу
// Параметры:
//  СтруктураКолонок - Структура -  Ключи задают имена колонок. Значение задает типизацию. 
//  Данные			 - Строка -  Текст для загрузки в таблицу.
// Возвращаемое значение:
//  ТаблицаЗначений -

Функция СоздатьТаблицу(СтруктураКолонок, Данные) Экспорт

	ТаблицаДанных = Новый ТаблицаЗначений;
	
	Колонки = ТаблицаДанных.Колонки;
	Для каждого Описание Из СтруктураКолонок Цикл
		
		Если ТипЗнч(Описание.Значение) = тип("ОписаниеТипов") Тогда
			
			Колонки.Добавить(Описание.Ключ, Описание.Значение);
			
		Иначе
			
			Колонки.Добавить(Описание.Ключ);
			
		КонецЕсли; 
		
	КонецЦикла;
	
	МассивСтрок = СтрРазделить( Данные, Символы.ПС );
	
	Для каждого Строчка Из МассивСтрок Цикл
		
		МассивДанных = СтрРазделить( Строчка, " , ", Ложь);
		СтрокаТаблицы = ТаблицаДанных.Добавить();
		
		Для Индекс = 0 По МассивДанных.ВГраница() Цикл
			
			Попытка 
				СтрокаТаблицы[Индекс] = МассивДанных[Индекс];
			Исключение     
				СтрокаОшибки = СтрШаблон( "Ошибка в строке %1.", Строчка, ОписаниеОшибки() );  
				Сообщить(СтрокаОшибки);
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаДанных;

КонецФункции // СоздатьТаблицу()


//Создание визуальной таблицы
Функция ТаблДок( ТаблицаДанных ) Экспорт

	Если Ложь Тогда
		 ТаблицаДанных = Новый ТаблицаЗначений;
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;
	
	//Шапка
	Колонки = ТаблицаДанных.Колонки;
	
	НомерСтроки = 1;
	НомерКолонки = 1;
	Для каждого ТекущаяКолонка Из Колонки Цикл
	
		Ячейка = ТабДок.Область(НомерСтроки, НомерКолонки);
		Ячейка.Текст = ТекущаяКолонка.Имя;
		НомерКолонки = НомерКолонки + 1;
	
	КонецЦикла;
	
	//Тело таблицы
	Для каждого СтрокаТаблицы Из ТаблицаДанных Цикл
	
		НомерСтроки = ТабДок.ВысотаТаблицы + 1;
		Для НомерКолонки = 1 По Колонки.Количество() Цикл
		
			Ячейка =  ТабДок.Область(НомерСтроки, НомерКолонки);
			Ячейка.Текст = СтрокаТаблицы[НомерКолонки - 1];
		
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции // ТаблДок()
